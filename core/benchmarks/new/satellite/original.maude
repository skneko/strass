fmod TIME is
    protecting INT .

    sorts Time TimeOrInf Infinity .
    subsort Int < Time .
    subsorts Infinity Time < TimeOrInf .
    op +inf : -> Infinity .

    sort TimeInterval .
    op [_,_] : Time TimeOrInf -> TimeInterval [ctor] .

    var T : Time .
    
    eq +inf + T = +inf .
    eq +inf - T = +inf .
    eq T - +inf = +inf .
    eq +inf < T = false .
    eq +inf <= T = false .
    eq +inf >= T = true .
    eq +inf > T = true .
endfm

mod SATELLITE is
    protecting EXT-BOOL .
    protecting TIME .

    sorts PointingMode PointingModeState .
    ops earth slewing science comm maintenance : -> PointingMode [ctor] .
    op pm:__ : Time PointingMode -> PointingModeState [ctor format(m! oys os o)] .

    sort GroundVisibility GroundVisibilityState .
    ops visible notVisible : -> GroundVisibility [ctor] .
    op gv:__ : Time GroundVisibility -> GroundVisibilityState [ctor format(m! oys os o)] .

    sort InstrumentStatus InstrumentStatusState .
    ops idle warmup process turnoff : -> InstrumentStatus [ctor] .
    op is:__ : Time InstrumentStatus -> InstrumentStatusState [ctor format(m! oys os o)] .

    sort State .
    op {_$_,_,_} : Nat PointingModeState GroundVisibilityState InstrumentStatusState -> State [ctor format(m r! or os m s m s ms o)] .

    sort StateVariable .
    subsorts PointingMode GroundVisibility InstrumentStatus < StateVariable .

    op duration : StateVariable -> TimeInterval .
    eq duration(earth) = [ 1, +inf ] .
    eq duration(slewing) = [ 30, 30 ] .
    eq duration(science) = [ 36, 58 ] .
    eq duration(comm) = [ 30, 50 ] .
    eq duration(maintenance) = [ 90, 90 ] .
    eq duration(visible) = [ 60, 100 ] .
    eq duration(notVisible) = [ 1, 100 ] .
    eq duration(idle) = [ 1, +inf ] .
    eq duration(warmup) = [ 5, 5 ] .
    eq duration(process) = [ 5, 5 ] .
    eq duration(turnoff) = [ 5, 5 ] .

    vars T Tmin Tmax : Time . var V : StateVariable .

    op canContinue : Time TimeInterval -> Bool .
    eq canContinue(T, [ Tmin, Tmax ]) = T <= Tmax .
    eq canContinue(T, [ Tmin, +inf ]) = true .

    op canChange : Time TimeInterval -> Bool .
    eq canChange(T, [ Tmin, Tmax:TimeOrInf ]) = T >= Tmin .

    op canContinue : Time StateVariable -> Bool .
    eq canContinue(T, V) = canContinue(T, duration(V)) .

    op canChange : Time StateVariable -> Bool .
    eq canChange(T, V) = canChange(T, duration(V)) .

    vars Tpm Tgv Tis : Time . var PM : PointingMode . var GV : GroundVisibility . var IS : InstrumentStatus .
    vars N : Nat .

    crl [advance-time] : { N $ pm: Tpm PM, gv: Tgv GV, is: Tis IS } => { sd(N, 1) $ pm: (Tpm + 1) PM, gv: (Tgv + 1) GV, is: (Tis + 1) IS }
        if N > 0 and-then canContinue(Tpm + 1, PM) and-then canContinue(Tgv + 1, GV) and-then canContinue(Tis + 1, IS) .

    crl [pm-earth-slewing] :        pm: T earth       => pm: 0 slewing     if canChange(T, earth) .
    crl [pm-earth-comm] :           pm: T earth       => pm: 0 comm        if canChange(T, earth) .
    crl [pm-earth-maintenance] :    pm: T earth       => pm: 0 maintenance if canChange(T, earth) .
    crl [pm-slewing-earth] :        pm: T slewing     => pm: 0 earth       if canChange(T, slewing) .
    crl [pm-slewing-science] :      pm: T slewing     => pm: 0 science     if canChange(T, slewing) .
    crl [pm-science-slewing] :      pm: T science     => pm: 0 slewing     if canChange(T, science) .
    crl [pm-comm-earth] :           pm: T comm        => pm: 0 earth       if canChange(T, comm) .
    crl [pm-comm-maintenance] :     pm: T comm        => pm: 0 maintenance if canChange(T, comm) .
    crl [pm-maintenance-earth] :    pm: T maintenance => pm: 0 earth       if canChange(T, comm) .

    crl [gv-visible-notVisible] :   gv: T visible     => gv: 0 notVisible  if canChange(T, visible) .
    crl [gv-notVisible-visible] :   gv: T notVisible  => gv: 0 visible     if canChange(T, notVisible) .

    crl [is-idle-warmup] :          is: T idle        => is: 0 warmup      if canChange(T, idle) .
    crl [is-idle-process] :         is: T idle        => is: 0 process     if canChange(T, idle) .
    crl [is-warmup-idle] :          is: T warmup      => is: 0 idle        if canChange(T, warmup) .
    crl [is-warmup-process] :       is: T warmup      => is: 0 process     if canChange(T, warmup) .
    crl [is-process-idle] :         is: T process     => is: 0 idle        if canChange(T, process) .
    crl [is-process-turnoff] :      is: T process     => is: 0 turnoff     if canChange(T, process) .
    crl [is-turnoff-idle] :         is: T turnoff     => is: 0 idle        if canChange(T, turnoff) .
endm