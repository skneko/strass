***(
    STRASS: assertion-based automatic theory repair tool

    Daniel Galán Pascual <dagapas@vrain.upv.es>
    DSIC-ELP, Universitat Politècnica de València
    March 2022
)

load preludex.maude
load pretty-print.maude

fmod STRASS-INSPECTION is
    pr META-MODULE .

    var M : Module .
    var H : Header . var Q : Qid . var Is : ImportList . 
    var Ss : SortSet . var SubSs : SubsortDeclSet . 
    var Ops : OpDeclSet . 
    var MAs  : MembAxSet . 
    var Es : EquationSet . var Rs : RuleSet .

    op collectRuleLabels : Module -> QidSet .
    eq collectRuleLabels(mod H is Is sorts Ss . SubSs Ops MAs Es Rs endm) =
        collectRuleLabels(Rs) .

    vars T T' : Term . var RC : Condition . var Ats : AttrSet .
    var QL : Qid .

    op collectRuleLabels : RuleSet -> QidSet .
    eq collectRuleLabels((rl T => T' [label(QL) Ats] .) Rs) = QL ; collectRuleLabels(Rs) .
    eq collectRuleLabels((rl T => T' [Ats] .) Rs) = collectRuleLabels(Rs) [owise] .
    eq collectRuleLabels((crl T => T' if RC [label(QL) Ats] .) Rs) = QL ; collectRuleLabels(Rs) .
    eq collectRuleLabels((crl T => T' if RC [Ats] .) Rs) = collectRuleLabels(Rs) [owise] .
    eq collectRuleLabels(none) = none .
endfm

fmod STRASS-CONSTRAINTS-BASE is
    pr META-STRATEGY .
    pr NAT .

    sorts StateConstraint PathConstraint Constraint IndexedConstraint .
    subsort StateConstraint PathConstraint < Constraint .

    sort Index .
    subsort Nat < Index .
    op global : -> Index .

    op _#_ : Term Term -> StateConstraint [ctor] .
    op path_ : Strategy -> PathConstraint [ctor] .
    op [_]_ : Index Constraint -> IndexedConstraint [ctor] .
endfm

view IndexedConstraint from TRIV to STRASS-CONSTRAINTS-BASE is
    sort Elt to IndexedConstraint .
endv

view Sort from TRIV to STRASS-CONSTRAINTS-BASE is
    sort Elt to Sort .
endv

fmod STRASS-META-CONSTRAINT-GUARD-OPERATORS is
    pr EXT-BOOL .
endfm

fmod STRASS-META-CONSTRAINT-ACTION-LANGUAGE is
    sort __STRASS_Action __STRASS_RuleLabel __STRASS_RuleLabelSet .
    subsort __STRASS_RuleLabel < __STRASS_Action .
    subsort __STRASS_RuleLabel < __STRASS_RuleLabelSet .

    op _,_ : __STRASS_RuleLabelSet __STRASS_RuleLabelSet -> __STRASS_RuleLabelSet [ctor assoc comm id: none] .
    op none : -> __STRASS_RuleLabelSet [ctor] .

    op idle : -> __STRASS_Action [ctor] .
    op all : -> __STRASS_Action [ctor] .
    op all except `{_`} : __STRASS_RuleLabelSet -> __STRASS_Action [ctor] .
    op _|_ : __STRASS_Action __STRASS_Action -> __STRASS_Action [ctor assoc comm id: idle prec 11 gather (e E)] .
    op _;_ : __STRASS_Action __STRASS_Action -> __STRASS_Action [ctor assoc id: idle prec 9 gather (e E)] .
    op _* : __STRASS_Action -> __STRASS_Action [ctor] .
    op _+ : __STRASS_Action -> __STRASS_Action [ctor] .
    op _! : __STRASS_Action -> __STRASS_Action [ctor] .
endfm

fmod STRASS-CONSTRAINTS-PARSING is
    pr STRASS-INSPECTION .
    pr STRASS-CONSTRAINTS-BASE .

    pr META-LEVEL-EXT .
    pr LEXICAL .
    pr STRING .
    pr MAP{Sort, IndexedConstraint} * (
        sort Map{Sort, IndexedConstraint} to TaggedConstraintSet,
        sort Entry{Sort, IndexedConstraint} to TaggedConstraint) .

    ***( Constraint expression parsing )***

    op incorrectGlobalsParseFailure : -> [TaggedConstraintSet] [format (r! o)] .
    op constraintParseFailure : Nat -> [TaggedConstraint] [format (r! o)] .
    op actionParseFailure : -> [Strategy] [format (r! o)] .

    var LineNum : Nat .

    eq constraintParseFailure(LineNum), TC:TaggedConstraint = constraintParseFailure(LineNum) .

    vars M MP MPx MT : Module . var Raw : String .
    var Ty Ty' : Type .

    op parseConstraints : Module Module String ~> TaggedConstraintSet .
    eq parseConstraints(M, MP, Raw) =
        if $checkGlobals($parseConstraints(M, MP, Raw)) 
        then $parseConstraints(M, MP, Raw)
        else incorrectGlobalsParseFailure 
        fi .
    
    op $parseConstraints : Module Module String ~> TaggedConstraintSet .
    eq $parseConstraints(M, MP, Raw) = 
        $$parseConstraints(
            M, 
            mergeModules(extendWithGuardOperators(MP), M),
            $generateParsingContext(M, mergeModules(extendWithGuardOperators(MP), M)),
            1,
            Raw) .

    op $$parseConstraints : Module Module Module Nat String ~> TaggedConstraintSet .
    eq $$parseConstraints(M, MPx, MT, LineNum, Raw) =   
        if find(Raw, "\n", 0) == notFound then $parseOneConstraint(M, MPx, MT, LineNum, Raw)
        else
            $parseOneConstraint(M, MPx, MT, LineNum, substr(Raw, 0, find(Raw, "\n", 0))),
            if length(substr(Raw, find(Raw, "\n", 0) + 1, length(Raw))) == 0 
            then empty
            else
                $$parseConstraints(M, MPx, MT, LineNum + 1, substr(Raw, find(Raw, "\n", 0) + 1, length(Raw)))
            fi
        fi .

    op $parseOneConstraint : Module Module Module Nat String ~> TaggedConstraint .
    eq $parseOneConstraint(M, MPx, MT, LineNum, Raw) =
        $$parseOneConstraint(M, MPx, LineNum, 
            metaParse(MT, tokenize(Raw), '__STRASS_MonomorphizedConstraint)) .

    op $$parseOneConstraint : Module Module Nat ResultPair? ~> TaggedConstraint .
    ceq $$parseOneConstraint(M, MPx, LineNum, R:ResultPair) =
        $$$parseOneConstraint(LineNum,
            metaParse(M, metaPrettyPrint(M, P:Term), anyType),
            metaParse(MPx, metaPrettyPrint(MPx, G:Term), 'Bool))
    if { '_#_ [ P:Term, G:Term ] , '__STRASS_MonomorphizedConstraint } := R:ResultPair .
    ceq $$parseOneConstraint(M, MPx, LineNum, R:ResultPair) =
        $$$parseOneConstraint(M, LineNum, ATy:Term, A:Term)
    if { 'path`for_:_ [ ATy:Term, A:Term ] , '__STRASS_MonomorphizedConstraint } := R:ResultPair .
    eq $$parseOneConstraint(M, MPx, LineNum, R:[ResultPair?]) = constraintParseFailure(LineNum) [owise] .

    op $$$parseOneConstraint : Nat ResultPair? ResultPair? ~> TaggedConstraint .
    ceq $$$parseOneConstraint(LineNum, RP:ResultPair, RG:ResultPair) =
        toSort(Ty) |-> [LineNum] P:Term # G:Term
    if { P:Term, Ty } := RP:ResultPair /\ { G:Term, Ty' } := RG:ResultPair .
    eq $$$parseOneConstraint(LineNum, RP:[ResultPair?], RG:[ResultPair?]) = constraintParseFailure(LineNum) [owise] .

    op $$$parseOneConstraint : Module Nat Term Term ~> TaggedConstraint .
    ceq $$$parseOneConstraint(M, LineNum, TTy:Term, TA:Term) = toSort(Ty) |-> [global] path A:Strategy 
    if A:Strategy := downAction(TA:Term, collectRuleLabels(M))
    /\ Ty := downConstant(TTy:Term, '__STRASS_SortName) .
    eq $$$parseOneConstraint(M, LineNum, TATy:[Term], A:[Term]) = constraintParseFailure(LineNum) [owise] .

    ***( Handle globals )***

    var TCs : TaggedConstraintSet .

    op $checkGlobals : TaggedConstraintSet -> Bool .
    eq $checkGlobals((Tag:Sort |-> [global] C:Constraint, Tag':Sort |-> [global] C':Constraint, TCs)) = false .
    eq $checkGlobals(TCs) = true [owise] . 

    ***( Action sublanguage parsing )***

    vars T T' : Term .
    var RlLs : QidSet .

    op downAction : Term QidSet ~> Strategy .
    eq downAction('idle.__STRASS_Action, RlLs) = idle .
    eq downAction('all.__STRASS_Action, RlLs) = all .
    eq downAction('all`except`{_`} [ T ], RlLs) = $stratLabelOr(RlLs \ $downToQidSet(T)) . 
    eq downAction('_|_ [ T , T' ], RlLs) = downAction(T, RlLs) | downAction(T', RlLs) .
    eq downAction('_;_ [ T , T' ], RlLs) = downAction(T, RlLs) ; downAction(T', RlLs) .
    eq downAction('_*[ T ], RlLs) = downAction(T, RlLs) * .
    eq downAction('_+[ T ], RlLs) = downAction(T, RlLs) + .
    eq downAction('_![ T ], RlLs) = downAction(T, RlLs) ! .
    eq downAction(QL:Qid, RlLs) = downConstant(QL:Qid, '__STRASS_RuleLabel)[none]{empty} .
    eq downAction(T, RlLs) = actionParseFailure [owise] .

    op $stratLabelOr : QidSet -> Strategy .
    eq $stratLabelOr((Q ; Qs)) = Q[none]{empty} | $stratLabelOr(Qs) .
    eq $stratLabelOr(none) = fail .

    op $downToQidSet : Term ~> QidSet .
    ceq $downToQidSet('_`,_ [ T , T' ]) = Q ; $downToQidSet(T') if Q := downConstant(T, '__STRASS_RuleLabel) .
    ceq $downToQidSet(T) = Q if Q := downConstant(T, '__STRASS_RuleLabel) .
    eq $downToQidSet('none) = none .

    vars A A' A'' Astep : Strategy .

    op interleave : Strategy Strategy -> Strategy .
    ceq interleave(A | A', Astep) = interleave(A, Astep) | interleave(A', Astep) if A =/= fail /\ A' =/= fail .
    ceq interleave(A ; A', Astep) = interleave(A, Astep) ; interleave(A', Astep) if A =/= idle /\ A' =/= idle .
    eq interleave(A or-else A', Astep) = interleave(A, Astep) or-else interleave(A', Astep) .
    eq interleave(A *, Astep) = interleave(A, Astep) * .
    eq interleave(A +, Astep) = interleave(A, Astep) + .
    eq interleave(A !, Astep) = interleave(A, Astep) ! .
    eq interleave(A ? A' : A'', Astep) = interleave(A, Astep) ? interleave(A', Astep) : interleave(A'', Astep) .
    eq interleave(one(A), Astep) = one(interleave(A, Astep)) .
    eq interleave(not(A), Astep) = not(interleave(A, Astep)) .
    eq interleave(test(A), Astep) = test(interleave(A, Astep)) .
    eq interleave(try(A), Astep) = try(interleave(A, Astep)) .
    eq interleave(RA:RuleApplication, Astep) = RA:RuleApplication ; Astep .
    eq interleave(A, Astep) = A [owise] .

    ***( Module and parsing context handling )***

    op extendWithGuardOperators : Module -> Module .
    eq extendWithGuardOperators(M) =
        mergeModules(M, upModule('STRASS-META-CONSTRAINT-GUARD-OPERATORS, false)) .

    op $generateParsingContext : Module Module -> Module .
    eq $generateParsingContext(M, MPx) = 
        mergeModules(
            mergeModules(
                mod 'STRASS-TRANSIENT-PARSING-CONTEXT is
                    nil
                    sorts '__STRASS_MonomorphizedConstraint .
                    none
                    $generateParsingContextOps(MPx, maximalSorts(MPx), '__STRASS_MonomorphizedConstraint)
                    none
                    none
                    none
                endm,
                MPx),
            $generateActionParsingContext(M)) .

    vars Ss Ss' : SortSet . vars S S' ST : Sort .
    var K : Kind .

    op $generateParsingContextOps : Module SortSet Sort -> OpDeclSet .
    eq $generateParsingContextOps(M, Ss, ST) = 
        $generateParsingContextOps(M, Ss, 'Bool, ST) .

    op $generateParsingContextOps : Module SortSet SortSet Sort -> OpDeclSet .
    eq $generateParsingContextOps(M, S ; Ss, Ss', ST) =
        $$generateParsingContextOps(getKind(M, S), Ss', ST)
        $generateParsingContextOps(M, Ss, Ss', ST) .
    eq $generateParsingContextOps(M, none, Ss', ST) = none .

    op $$generateParsingContextOps : Kind SortSet Sort -> OpDeclSet .
    eq $$generateParsingContextOps(K, S' ; Ss', ST) =
        (op '_#_ : K S' -> ST [ctor prec(0) gather('& '&)] .)
        (op 'path`for_:_ : '__STRASS_SortName '__STRASS_Action -> ST [ctor prec(0) gather('& '&)] .)
        $$generateParsingContextOps(K, Ss', ST) .
    eq $$generateParsingContextOps(K, none, ST) = none .

    op $generateActionParsingContext : Module -> Module .
    eq $generateActionParsingContext(M) =
        mergeModules(
            mod 'STRASS-TRANSIENT-ACTION-PARSING-CONTEXT is
                nil
                sorts '__STRASS_RuleLabel ; '__STRASS_SortName .
                none
                $generateActionParsingContextOpsRuleLabels(collectRuleLabels(M), '__STRASS_RuleLabel)
                $generateActionParsingContextOpsSortNames(getSorts(M), '__STRASS_SortName)
                none
                none
                none
            endm,
            upModule('STRASS-META-CONSTRAINT-ACTION-LANGUAGE, false)) .

    var Q : Qid . var Qs : QidSet .

    op $generateActionParsingContextOpsRuleLabels : QidSet Sort -> OpDeclSet .
    eq $generateActionParsingContextOpsRuleLabels(Q ; Qs, S) =
        (op Q : nil -> S [ctor] .)
        $generateActionParsingContextOpsRuleLabels(Qs, S) .
    eq $generateActionParsingContextOpsRuleLabels(none, S) = none .

    op $generateActionParsingContextOpsSortNames : SortSet Sort -> OpDeclSet .
    eq $generateActionParsingContextOpsSortNames(S ; Ss, S') =
        (op S : nil -> S' [ctor] .)
        $generateActionParsingContextOpsSortNames(Ss, S') .
    eq $generateActionParsingContextOpsSortNames(none, S') = none .
endfm

fmod STRASS-CONSTRAINTS is
    pr STRASS-CONSTRAINTS-PARSING .

    var S : Sort . var NIdx : Nat .
    var CS : StateConstraint . var CP : PathConstraint .
    var TCs : TaggedConstraintSet .

    op containsStateConstraints : TaggedConstraintSet -> Bool .
    eq containsStateConstraints((S |-> [NIdx] CS, TCs)) = true .
    eq containsStateConstraints(TCs) = false [owise] .

    op containsPathConstraints : TaggedConstraintSet -> Bool .
    eq containsPathConstraints((S |-> [NIdx] CP, TCs)) = true .
    eq containsPathConstraints(TCs) = false [owise] .

    op hasGlobalPathFor : Sort TaggedConstraintSet -> Bool .
    eq hasGlobalPathFor(S, (S |-> [global] CP, TCs)) = true .
    eq hasGlobalPathFor(S, TCs) = false [owise] .
endfm

view SortSet from TRIV to STRASS-CONSTRAINTS is
    sort Elt to SortSet .
endv

fmod STRASS-ANALYSIS-DEPENDENCIES is
    pr META-LEVEL-EXT .

    pr MAP{Sort, SortSet} * (sort Map{Sort, SortSet} to SortDependencyMap) .

    ***( Dependency map generation )***

    var M : Module .
    var SDM : SortDependencyMap .

    op arrangedSortDependencies : Module -> SortDependencyMap .
    eq arrangedSortDependencies(M) = maximal(transitive(sortDependencies(M)), M) .

    op maximal : SortDependencyMap Module -> SortDependencyMap .
    eq maximal(SDM, M) = $filterByKey(maximalSorts(M), SDM) .

    op transitive : SortDependencyMap -> SortDependencyMap .
    eq transitive(SDM) = transitiveClosure(SDM) .

    op sortDependencies : Module -> SortDependencyMap .
    eq sortDependencies(M) =
        $mergeKeys((
            subsortDependencies(M, getSorts(M)),
            usageDependencies(getSorts(M), getOps(M)))) .

    var S : Sort . var Ss : SortSet .
    var Q : Qid . var Ops : OpDeclSet . var Ats : AttrSet .

    op subsortDependencies : Module SortSet -> SortDependencyMap .
    eq subsortDependencies(M, S ; Ss) = 
        S |-> lesserSorts(M, S),
        subsortDependencies(M, Ss) .
    eq subsortDependencies(M, none) = empty .

    op usageDependencies : SortSet OpDeclSet -> SortDependencyMap .
    eq usageDependencies(S ; Ss, Ops) =
        S |-> $usageDependencies(S, Ops),
        usageDependencies(Ss, Ops) .
    eq usageDependencies(none, Ops) = empty .

    op $usageDependencies : Sort OpDeclSet -> SortSet .
    eq $usageDependencies(S, (op Q : Args:TypeList -> Ret:Type [Ats] .) Ops) =
        if toSort(Ret:Type) == S then $$usageDependencies(S, Args:TypeList)
        else none 
        fi ;
        $usageDependencies(S, Ops) .
    eq $usageDependencies(S, none) = none .

    var Ty : Type . var Tys : TypeList .

    op $$usageDependencies : Sort TypeList -> SortSet .
    eq $$usageDependencies(S, Ty Tys) = 
        if toSort(Ty) =/= S then toSort(Ty)
        else none fi ; 
        $$usageDependencies(S, Tys) .
    eq $$usageDependencies(S, nil) = none .

    var SDM' : SortDependencyMap .

    op transitiveClosure : SortDependencyMap -> SortDependencyMap .
    eq transitiveClosure(SDM) = $transitiveClosure(SDM, SDM) .

    op $transitiveClosure : SortDependencyMap SortDependencyMap -> SortDependencyMap .
    eq $transitiveClosure((S |-> Ss, SDM), SDM') =
        S |-> $$transitiveClosure(Ss, SDM'), 
        $transitiveClosure(SDM, SDM') .
    eq $transitiveClosure(empty, SDM') = empty .

    op $$transitiveClosure : SortSet SortDependencyMap -> SortSet .
    eq $$transitiveClosure((S ; Ss), SDM) =
        S ;
        if SDM[S] =/= undefined then SDM[S]
        else none
        fi ;
        $$transitiveClosure(Ss, SDM) .
    eq $$transitiveClosure(none, SDM) = none .

    ***( Auxiliary functions )***

    var Ss' : SortSet .

    op $mergeKeys : SortDependencyMap -> SortDependencyMap .
    eq $mergeKeys((S |-> Ss, S |-> Ss', SDM)) = S |-> (Ss ; Ss'), $mergeKeys(SDM) .
    eq $mergeKeys(SDM) = SDM [owise] .

    op $filterByKey : SortSet SortDependencyMap -> SortDependencyMap .
    eq $filterByKey(Ss, (S |-> Ss', SDM)) =
        if S in Ss then S |-> Ss'
        else empty
        fi,
        $filterByKey(Ss, SDM) .
    eq $filterByKey(Ss, (empty).SortDependencyMap) = empty .
endfm

fmod STRASS-ANALYSIS is
    pr STRASS-CONSTRAINTS .
    pr STRASS-ANALYSIS-DEPENDENCIES .
endfm

fmod STRASS-GENERATION is
    pr STRASS-ANALYSIS .
    pr CONVERSION .

    ***( Strategy body generation )***

    var SDM : SortDependencyMap . var S : Sort . var Ss : SortSet .
    var TCs : TaggedConstraintSet . var NIdx : Nat . 
    var C : Constraint . var CS : StateConstraint .

    op makeStratDeclSet : SortDependencyMap TaggedConstraintSet -> StratDeclSet .
    eq makeStratDeclSet(SDM, TCs) = 
        makeConstraintStratDeclSet(TCs)
        makeSortRelatedStratDeclSet(SDM, TCs) .

    op makeConstraintStratDeclSet : TaggedConstraintSet -> StratDeclSet .
    eq makeConstraintStratDeclSet((S |-> [NIdx] CS, TCs)) =
        (strat $stateStratName(NIdx) : nil @ S [ none ] .)
        makeConstraintStratDeclSet(TCs) .
    eq makeConstraintStratDeclSet((S |-> [global] C, TCs)) = makeConstraintStratDeclSet(TCs) .
    eq makeConstraintStratDeclSet(empty) = none .

    op makeSortRelatedStratDeclSet : SortDependencyMap TaggedConstraintSet -> StratDeclSet .
    eq makeSortRelatedStratDeclSet((S |-> Ss, SDM), TCs) = 
        (strat S : nil @ S [ none ] .)
        if hasGlobalPathFor(S, TCs)
        then (strat $suffix(S, "-state") : nil @ S [ none ] .)
        else none
        fi
        makeSearchStratDecls(S)
        makeSortRelatedStratDeclSet(SDM, TCs) .
    eq makeSortRelatedStratDeclSet(empty, TCs) = none .

    op makeSearchStratDecls : Sort -> StratDeclSet .
    eq makeSearchStratDecls(S) =
        (strat $suffix(S, "*") : nil @ S [ none ] .)
        (strat $suffix(S, "+") : nil @ S [ none ] .)
        (strat $suffix(S, "!") : nil @ S [ none ] .) .

    vars P G : Term . var A : Strategy .

    op makeStratDefSet : SortDependencyMap TaggedConstraintSet -> StratDefSet .
    eq makeStratDefSet(SDM, TCs) =
        makeConstraintStratDefSet(TCs)
        makeSortRelatedStratDefs(SDM, TCs) .

    op makeConstraintStratDefSet : TaggedConstraintSet -> StratDefSet .
    eq makeConstraintStratDefSet((S |-> [NIdx] P # G, TCs)) =
        makeStateStratDef(NIdx, P, G)
        makeConstraintStratDefSet(TCs) .
    eq makeConstraintStratDefSet((S |-> [global] C, TCs)) = makeConstraintStratDefSet(TCs) .
    eq makeConstraintStratDefSet(empty) = none .

    op makeStateStratDef : Nat Term Term -> StratDefinition .
    eq makeStateStratDef(NIdx, P, G) =
        (sd $stateStratName(NIdx)[[empty]] := makeStateStratBody(P, G) [ none ] .) .

    op makeStateStratBody : Term Term -> Strategy .
    eq makeStateStratBody(P, G) = not (amatch P s.t. (G = 'false.Bool)) .

    op makeSortRelatedStratDefs : SortDependencyMap TaggedConstraintSet -> StratDefSet .
    eq makeSortRelatedStratDefs((S |-> Ss, SDM), TCs) =
        makeLocalStratDefs(S, $filterByKey(S ; Ss, TCs))
        makeSearchStratDefs(S, $filterByKey(S ; Ss, TCs) == empty)
        makeSortRelatedStratDefs(SDM, TCs) .
    eq makeSortRelatedStratDefs(empty, TCs) = none .

    op makeLocalStratDefs : Sort TaggedConstraintSet -> StratDefSet .
    eq makeLocalStratDefs(S, TCs) =
        if hasGlobalPathFor(S, TCs) 
        then
            (sd S[[empty]] := $makeLocalStratDefPath(S, TCs) [ none ] .)
            (sd $suffix(S, "-state")[[empty]] := $makeLocalStratDefState(TCs) [ none ] .)
        else
            (sd S[[empty]] := $makeLocalStratDefState(TCs) [ none ] .)
        fi .

    op $makeLocalStratDefState : TaggedConstraintSet -> Strategy .
    eq $makeLocalStratDefState((S |-> [NIdx] CS, TCs)) =
        $stateStratName(NIdx)[[empty]] ; $makeLocalStratDefState(TCs) .
    eq $makeLocalStratDefState((S |-> [global] C, TCs)) = $makeLocalStratDefState(TCs) .
    eq $makeLocalStratDefState(TCs) = idle [owise] .

    op $makeLocalStratDefPath : Sort TaggedConstraintSet -> Strategy .
    ceq $makeLocalStratDefPath(S, TCs) = interleave(A, $suffix(S, "-state")[[empty]])
    if (S |-> [global] path A, Tail:TaggedConstraintSet) := TCs .

    op makeSearchStratDefs : Sort Bool -> StratDefSet .
    eq makeSearchStratDefs(S, Simple:Bool) =
        if Simple:Bool then
            (sd $suffix(S, "*")[[empty]] := all * [ none ] .)
            (sd $suffix(S, "+")[[empty]] := all + [ none ] .)
            (sd $suffix(S, "!")[[empty]] := all ! [ none ] .)
        else
            (sd $suffix(S, "*")[[empty]] := S[[empty]] ; ((all ; S[[empty]]) *) [ none ] .)
            (sd $suffix(S, "+")[[empty]] := S[[empty]] ; ((all ; S[[empty]]) +) [ none ] .)
            (sd $suffix(S, "!")[[empty]] := S[[empty]] ; ((all ; S[[empty]]) !) [ none ] .)
        fi .

    ***( Strategy module wrapping )***

    vars H OrigH HP : Header . var Is : ImportList .
    var SDcs : StratDeclSet . var SDfs : StratDefSet .

    op makeStratModule : Header Header SortDependencyMap TaggedConstraintSet -> StratModule .
    eq makeStratModule(OrigH, HP, SDM, TCs) =
        makeStratModule(makeHeader(OrigH), makeImportList(OrigH, HP), SDM, TCs) .

    op makeStratModule : Header ImportList SortDependencyMap TaggedConstraintSet -> StratModule .
    eq makeStratModule(H, Is, SDM, TCs) =
        makeStratModule(H, Is, makeStratDeclSet(SDM, TCs), makeStratDefSet(SDM, TCs)) .

    op makeStratModule : Header ImportList StratDeclSet StratDefSet -> StratModule .
    eq makeStratModule(H, Is, SDcs, SDfs) =
        smod H is
            Is                              --- ImportList
            sorts none .                    --- SortSet
            none                            --- SubsortDeclSet 
            none                            --- OpDeclSet 
            none                            --- MembAxSet 
            none                            --- EquationSet 
            none                            --- RuleSet 
            SDcs                            --- StratDeclSet 
            SDfs                            --- StratDefSet
        endsm .

    op makeHeader : Header -> Header .
    eq makeHeader(OrigH) = qid(string(OrigH) + "-STRAT") .

    op makeImportList : Header Header -> ImportList .
    eq makeImportList(OrigH, HP) = (protecting OrigH .) (protecting HP .) .

    ***( Auxiliary functions )***

    op $stateStratName : Nat -> Qid .
    eq $stateStratName(NIdx) = qid("s" + string(NIdx, 10)) .

    op $suffix : Qid String -> Qid .
    eq $suffix(Q:Qid, S:String) = qid(string(Q:Qid) + S:String) .

    var Idx : Index .

    op $filterByKey : SortSet TaggedConstraintSet -> TaggedConstraintSet .
    eq $filterByKey(Ss, (S |-> [Idx] C, TCs)) =
        if S in Ss then S |-> [Idx] C
        else empty
        fi,
        $filterByKey(Ss, TCs) .
    eq $filterByKey(Ss, (empty).TaggedConstraintSet) = empty .
endfm

fmod STRASS-FORMATTING is
    pr STRASS-GENERATION .
    pr PRETTY-PRINT .

    vars MP MS : Module . var OrigH : Header .

    op programContents : Header Module Module -> String .
    eq programContents(OrigH, MP, MS) = 
        programHeader(OrigH) 
        + "\n--- Notice: extended by STRASS, not the original definition\n" + toString(MP)
        + "\n" + toString(MS) .

    op programHeader : Header -> String .
    eq programHeader(OrigH) =
          "load " + toLower(string(OrigH)) + ".maude\t---Load files containing the original definitions.\n\n"
        + "***(\n\tGenerated by STRASS -- Daniel Galán <dagapas@vrain.upv.es>, 2021\n)\n" .
endfm

mod STRASS is
    pr STRASS-FORMATTING .

    vars M MP : Module . vars Q QP : Qid . var RawCs : String .
    vars H HP : Header . vars Is IsP : ImportList . 
    vars Ss SsP : SortSet . vars SubSs SubSsP : SubsortDeclSet . 
    vars Ops OpsP : OpDeclSet . 
    vars MAs MAsP : MembAxSet . 
    vars Es EsP : EquationSet . vars Rs RsP : RuleSet .

    op fix : Qid Qid String -> String .
    eq fix(Q, QP, RawCs) = 
        programContents(
            Q,
            removeDuplicateImports(
                extendWithGuardOperators(upModule(QP, false))),
            removeDuplicateImports(
                generateRepairStrats(Q, QP, RawCs))) .

    op generateRepairStrats : Qid Qid String -> StratModule .
    eq generateRepairStrats(Q, QP, RawCs) = 
        generateRepairStrats(upModule(Q, false), upModule(QP, false), RawCs) .

    op generateRepairStrats : Module Module String -> StratModule .
    ceq generateRepairStrats(M, MP, RawCs) =
        makeStratModule(H, HP, arrangedSortDependencies(M), parseConstraints(M, MP, RawCs))
    if (mod H is Is sorts Ss . SubSs Ops MAs Es Rs endm) := upgradeToSystemModule(M)
    /\ (mod HP is IsP sorts SsP . SubSsP OpsP MAsP EsP RsP endm) := upgradeToSystemModule(MP) .
endm
